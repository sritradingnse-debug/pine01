// LICENSE: CC0 (https://creativecommons.org/publicdomain/zero/1.0/)

//@version=6
indicator(title="Elder Impulse System - Volume Weighted", shorttitle="Elder Impulse VW", overlay=true)

source = close

// MACD Options
macd_length_fast   = input.int(defval=12, minval=1, title="MACD Fast Length")
macd_length_slow   = input.int(defval=26, minval=1, title="MACD Slow Length")
macd_length_signal = input.int(defval=9,  minval=1, title="MACD Signal Length")

// Calculate Volume-Weighted MACD
// VW-MACD = EMA(close*volume, fast) / EMA(volume, fast) - EMA(close*volume, slow) / EMA(volume, slow)
close_vol_fast     = ta.ema(source * volume, macd_length_fast)
vol_fast           = ta.ema(volume, macd_length_fast)
vw_ma_fast         = close_vol_fast / vol_fast

close_vol_slow     = ta.ema(source * volume, macd_length_slow)
vol_slow           = ta.ema(volume, macd_length_slow)
vw_ma_slow         = close_vol_slow / vol_slow

macd               = vw_ma_fast - vw_ma_slow
macd_signal        = ta.ema(macd, macd_length_signal)
macd_histogram     = macd - macd_signal

// EMA Option
ema_length         = input.int(defval=13, minval=1, title="EMA Length")
// Calculate EMA (using standard close, not volume-weighted for trend)
ema                = ta.ema(source, ema_length)

// Calculate Elder Impulse
elder_bulls        = (ema > ema[1]) and (macd_histogram > macd_histogram[1])
elder_bears        = (ema < ema[1]) and (macd_histogram < macd_histogram[1])
elder_color        = elder_bulls
                     ? color.green //  If Bulls Control Trend and Momentum
                     : elder_bears
                       ? color.red //  If Bears Control Trend and Momentum
                       : color.blue // If Neither Bulls or Bears Control the Market

barcolor(elder_color)
